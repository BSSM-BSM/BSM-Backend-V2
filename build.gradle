plugins {
	id 'org.springframework.boot' version '3.3.4'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'java'
}

group = 'bssm'
version = '2.1.0'
sourceCompatibility = '21'

repositories {
	mavenCentral()
	maven { url 'https://jitpack.io' }
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	// querydsl
	implementation 'com.querydsl:querydsl-jpa:5.1.0:jakarta'
	annotationProcessor 'com.querydsl:querydsl-apt:5.1.0:jakarta'
	annotationProcessor "jakarta.annotation:jakarta.annotation-api"
	annotationProcessor "jakarta.persistence:jakarta.persistence-api"

	// jwt
	implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

	// bsm oauth
	implementation 'com.github.leehj050211:bsm-oauth-java:1.1.0'

	// mariadb
	runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'

	// http client
	implementation 'com.squareup.okhttp3:okhttp:4.12.0'

	// json
	implementation 'com.google.code.gson:gson'

	// web push
	implementation 'nl.martijndwars:web-push:5.1.1'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	implementation 'com.google.guava:guava:33.3.0-jre'
	implementation 'org.bouncycastle:bcprov-jdk18on:1.78.1'
}

tasks.named('test') {
	useJUnitPlatform()
}

bootJar {
	archiveFileName = "app.jar"
}

// querydsl
def querydslDir = "$buildDir/generated/querydsl"

sourceSets {
	main.java.srcDirs += [ querydslDir ]
}

tasks.withType(JavaCompile).configureEach {
	options.getGeneratedSourceOutputDirectory().set(file(querydslDir));
}

clean.doLast {
	file(querydslDir).deleteDir()
}
